
setopt prompt_percent

# user@hostname will only be displayed on remote shells
PROMPT_HOST=${SSH_TTY:+'%F{white}(%F{green}%n%F{white}@%F{cyan}%m%F{white})'}

# (git info pwd) on rprompt
function update_rprompt() {
	local PROMPT_CWD_INFO=
	[[ "${PWD/#$HOME/~}" =~ '^~/?$' ]] || PROMPT_CWD_INFO="%F{green}%30<..<%~"

	local PROMPT_GIT_INFO=
	# TODO: change colors depending on the index status
	git branch &>/dev/null && PROMPT_GIT_INFO="%F{cyan}${$(git symbolic-ref HEAD 2>/dev/null)##*/} $(git rev-parse --short HEAD 2>/dev/null)"

	RPROMPT=
	[[ -z "${PROMPT_GIT_INFO}" && -z "${PROMPT_CWD_INFO}" ]] && return
	[[ ! -z "${PROMPT_GIT_INFO}" ]] && RPROMPT="${PROMPT_GIT_INFO}"
	[[ ! -z "${PROMPT_CWD_INFO}" ]] && RPROMPT="${PROMPT_CWD_INFO}"
	[[ ! -z "${PROMPT_GIT_INFO}" && ! -z "${PROMPT_CWD_INFO}" ]] && RPROMPT="${PROMPT_GIT_INFO} ${PROMPT_CWD_INFO}"

	RPROMPT="%F{white}(${RPROMPT}%F{white})%F{default}"
}

PROMPT="%F{white}[%F{yellow}%D{%H:%M:%S}%F{white}|%F{yellow}%h%F{white}]%(?. .%F{red}%?)${PROMPT_HOST}%F{blue}%#%F{default} "

function update_window_title() {
	echo -en "\e]1;zsh\a\e]2;${PWD/#$HOME/~}\a"
	[[ $(fc -ln -- -1 2>/dev/null) =~ '^git' ]] && update_rprompt
}

precmd_functions=( update_window_title )
chpwd_functions=( update_rprompt )

#execute each once on start
update_window_title
update_rprompt

# vim: syntax=sh:
